defmodule PasswordGeneratorTest do
  use ExUnit.Case
  

  setup do
    options = %{
      "length" => "10",
      "numbers" => "false",
      "symbols" => "false",
      "uppercase" => "false"
    }

    options_type = %{
      lowercase: Enum.map(?a..?z, &<<&1>>),
      uppercase: Enum.map(?A..?Z, &<<&1>>),
      numbers: Enum.map(?0..?9, &<<&1>>),
      symbols: String.split("!@#$%^&*()_+-=[]{}|;:,.<>?/~", "", trim: true)
    }

    {:ok, result} = PasswordGenerator.generate(options)

    %{
      options_type: options_type,
      result: result
    }
  end

  test "returns a string", %{result: result} do
    assert is_bitstring(result)
  end

  test "returns error when no length is given" do
    options = %{"invalid" => "false"}
    assert {:error, _error} = PasswordGenerator.generate(options)
  end

  test "returns error when length is not a number" do
    options = %{"length" => "ab"}
    assert {:error, _error} = PasswordGenerator.generate(options)
  end

  test "length of returned string is the option provided" do
    options = %{"length" => "5"}
    {:ok, result} = PasswordGenerator.generate(options)
    assert 5 = String.length(result)
  end

  test "returns only lowercase letters by default", %{
    options_type: options
  } do
    length_option = %{"length" => "5"}
    {:ok, result} = PasswordGenerator.generate(length_option)
    assert String.contains?(result, options.lowercase)

    refute String.contains?(result, options.uppercase)
    refute String.contains?(result, options.numbers)
    refute String.contains?(result, options.symbols)
  end

  test "returns error when option values are not booleans" do
    options = %{
      "length" => "10",
      "numbers" => "invalid",
      "symbols" => "false",
      "uppercase" => "0"
    }

    assert {:error, _error} = PasswordGenerator.generate(options)
  end

  test "returns error when options are not allowed" do
    options = %{"length" => "5", "invalid" => "true"}
    assert {:error, _error} = PasswordGenerator.generate(options)
  end

  test "returns error when one option is not allowed" do
    options = %{"length" => "5", "numbers" => "true", "invalid" => "true"}
    assert {:error, _error} = PasswordGenerator.generate(options)
  end
  test "returns string uppercase", %{options_type: options} do
    options_with_uppercase = %{
      "length" => "10",
      "numbers" => "false",
      "symbols" => "false",
      "uppercase" => "true"
    }
    {:ok, result} = PasswordGenerator.generate(options_with_uppercase)
    assert String.contains?(result, options.uppercase)

    refute String.contains?(result, options.numbers)
    refute String.contains?(result, options.symbols)
  end
end
